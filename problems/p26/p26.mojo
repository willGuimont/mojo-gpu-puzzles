# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #
from std.gpu import thread_idx, block_idx, block_dim, lane_id
from max.gpu.host import DeviceContext
from std.gpu.primitives.warp import shuffle_xor, prefix_sum, WARP_SIZE
from layout import TileTensor
from layout.tile_layout import row_major
from std.sys import argv
from std.testing import assert_equal, assert_almost_equal


comptime SIZE = WARP_SIZE
comptime BLOCKS_PER_GRID = (1, 1)
comptime THREADS_PER_BLOCK = (WARP_SIZE, 1)
comptime dtype = DType.float32
comptime layout = row_major[SIZE]()
comptime LayoutType = type_of(layout)


# ANCHOR: butterfly_pair_swap
def butterfly_pair_swap[
    size: Int
](
    output: TileTensor[mut=True, dtype, LayoutType, MutAnyOrigin],
    input: TileTensor[mut=False, dtype, LayoutType, ImmutAnyOrigin],
):
    """
    Basic butterfly pair swap: Exchange values between adjacent pairs using XOR pattern.
    Each thread exchanges its value with its XOR-1 neighbor, creating pairs: (0,1), (2,3), (4,5), etc.
    Uses shuffle_xor(val, 1) to swap values within each pair.
    This is the foundation of butterfly network communication patterns.
    """
    var global_i = block_dim.x * block_idx.x + thread_idx.x

    if global_i < size:
        var x = input[global_i]
        var y = shuffle_xor(x, 1)
        output[global_i] = y


# ANCHOR_END: butterfly_pair_swap


# ANCHOR: butterfly_parallel_max
def butterfly_parallel_max[
    size: Int
](
    output: TileTensor[mut=True, dtype, LayoutType, MutAnyOrigin],
    input: TileTensor[mut=False, dtype, LayoutType, ImmutAnyOrigin],
):
    """
    Parallel maximum reduction using butterfly pattern.
    Uses shuffle_xor with decreasing offsets (16, 8, 4, 2, 1) to perform tree-based reduction.
    Each step reduces the active range by half until all threads have the maximum value.
    This implements an efficient O(log n) parallel reduction algorithm.
    """
    var global_i = block_dim.x * block_idx.x + thread_idx.x

    if global_i < size:
        var max_value = input[global_i]
        var offset = WARP_SIZE // 2
        while offset > 0:
            var y = shuffle_xor(max_value, UInt32(offset))
            max_value = max(max_value, y)
            offset //= 2
        output[global_i] = max_value


# ANCHOR_END: butterfly_parallel_max


comptime SIZE_2 = 64
comptime BLOCKS_PER_GRID_2 = (2, 1)
comptime THREADS_PER_BLOCK_2 = (WARP_SIZE, 1)
comptime layout_2 = row_major[SIZE_2]()
comptime Layout2Type = type_of(layout_2)


# ANCHOR: butterfly_conditional_max
def butterfly_conditional_max[
    size: Int
](
    output: TileTensor[mut=True, dtype, Layout2Type, MutAnyOrigin],
    input: TileTensor[mut=False, dtype, Layout2Type, ImmutAnyOrigin],
):
    """
    Conditional butterfly maximum: Perform butterfly max reduction, but only store result
    in even-numbered lanes. Odd-numbered lanes store the minimum value seen.
    Demonstrates conditional logic combined with butterfly communication patterns.
    """
    var global_i = block_dim.x * block_idx.x + thread_idx.x
    var lane = lane_id()

    if global_i < size:
        var current_val = input[global_i]
        var max_val = current_val
        var min_val = current_val

        var offset = WARP_SIZE // 2
        while offset > 0:
            var y = shuffle_xor(max_val, UInt32(offset))
            max_val = max(max_val, y)

            y = shuffle_xor(min_val, UInt32(offset))
            min_val = min(min_val, y)
            offset //= 2

        if lane % 2 == 0:
            output[global_i] = max_val
        else:
            output[global_i] = min_val


# ANCHOR_END: butterfly_conditional_max


# ANCHOR: warp_inclusive_prefix_sum
def warp_inclusive_prefix_sum[
    size: Int
](
    output: TileTensor[mut=True, dtype, LayoutType, MutAnyOrigin],
    input: TileTensor[mut=False, dtype, LayoutType, ImmutAnyOrigin],
):
    """
    Inclusive prefix sum using warp primitive: Each thread gets sum of all elements up to and including its position.
    Compare this to Puzzle 14's complex shared memory + barrier approach.

    Puzzle 14 approach:
    - Shared memory allocation
    - Multiple barrier synchronizations
    - Log(n) iterations with manual tree reduction
    - Complex multi-phase algorithm

    Warp prefix_sum approach:
    - Single function call!
    - Hardware-optimized parallel scan
    - Automatic synchronization
    - O(log n) complexity, but implemented in hardware.

    NOTE: This implementation only works correctly within a single warp (WARP_SIZE threads).
    For multi-warp scenarios, additional coordination would be needed.
    """
    var global_i = block_dim.x * block_idx.x + thread_idx.x

    if global_i < size:
        var x = input[global_i]
        var s = prefix_sum(x)
        output[global_i] = s


# ANCHOR_END: warp_inclusive_prefix_sum


# ANCHOR: warp_partition
def warp_partition[
    size: Int
](
    output: TileTensor[mut=True, dtype, LayoutType, MutAnyOrigin],
    input: TileTensor[mut=False, dtype, LayoutType, ImmutAnyOrigin],
    pivot: Float32,
):
    """
    Single-warp parallel partitioning using BOTH shuffle_xor AND prefix_sum.
    This implements a warp-level quicksort partition step that places elements < pivot
    on the left and elements >= pivot on the right.

    ALGORITHM COMPLEXITY - combines two advanced warp primitives:
    1. shuffle_xor(): Butterfly pattern for warp-level reductions
    2. prefix_sum(): Warp-level exclusive scan for position calculation.

    This demonstrates the power of warp primitives for sophisticated parallel algorithms
    within a single warp (works for any WARP_SIZE: 32, 64, etc.).

    Example with pivot=5:
    Input:  [3, 7, 1, 8, 2, 9, 4, 6]
    var Result: [3, 1, 2, 4, 7, 8, 9, 6] (< pivot | >= pivot).
    """
    var global_i = block_dim.x * block_idx.x + thread_idx.x

    if global_i < size:
        var current_val = input[global_i]

        #        [3, 7, 1, 8, 2, 9, 4, 6]
        # left:  [1, 0, 1, 0, 1, 0, 1, 0]
        # right: [0, 1, 0, 1, 0, 1, 0, 1]
        var is_left = Scalar[dtype](1 if current_val < pivot else 0)
        var is_right = Scalar[dtype](1 if current_val >= pivot else 0)

        # Cummulative sum of the left and right before current position
        # left:  [1, 0, 1, 0, 1, 0, 1, 0]
        # sum:   [0, 1, 1, 2, 2, 3, 3, 4]
        # right: [0, 1, 0, 1, 0, 1, 0, 1]
        # sum:   [0, 0, 1, 1, 2, 2, 3, 3]
        # Basically the index (or offset for right) in the output
        # e.g., the 2 in the input should be at index 2 in the output
        # e.g., 9 is at index 2 in the right part (so index 2 + 4)
        var warp_left = prefix_sum[exclusive=True](is_left)
        var warp_right = prefix_sum[exclusive=True](is_right)

        # Compute total number of values smaller than pivot, used to compute right index
        var num_left = (
            is_left  # Start at is_left because warp_left is exclusive
        )
        var offset = WARP_SIZE // 2
        while offset > 0:
            num_left += shuffle_xor(num_left, UInt32(offset))
            offset //= 2

        # Store the output, see comments above
        if current_val < pivot:
            output[Int(warp_left)] = current_val
        else:
            output[Int(num_left + warp_right)] = current_val


# ANCHOR_END: warp_partition


def test_butterfly_pair_swap() raises:
    with DeviceContext() as ctx:
        var input_buf = ctx.enqueue_create_buffer[dtype](SIZE)
        input_buf.enqueue_fill(0)
        var output_buf = ctx.enqueue_create_buffer[dtype](SIZE)
        input_buf.enqueue_fill(0)

        with input_buf.map_to_host() as input_host:
            for i in range(SIZE):
                input_host[i] = Scalar[dtype](i)

        var input_tensor = TileTensor[mut=False, dtype, LayoutType](
            input_buf, layout
        )
        var output_tensor = TileTensor(output_buf, layout)

        comptime kernel = butterfly_pair_swap[SIZE]
        ctx.enqueue_function[kernel](
            output_tensor,
            input_tensor,
            grid_dim=BLOCKS_PER_GRID,
            block_dim=THREADS_PER_BLOCK,
        )

        var expected_buf = ctx.enqueue_create_host_buffer[dtype](SIZE)
        expected_buf.enqueue_fill(0)
        ctx.synchronize()

        # Create expected results: pairs should be swapped
        # (0,1) -> (1,0), (2,3) -> (3,2), (4,5) -> (5,4), etc.
        for i in range(SIZE):
            if i % 2 == 0:
                # Even positions get odd values
                expected_buf[i] = Scalar[dtype](i + 1)
            else:
                # Odd positions get even values
                expected_buf[i] = Scalar[dtype](i - 1)

        with output_buf.map_to_host() as output_host:
            print("output:", output_host)
            print("expected:", expected_buf)
            for i in range(SIZE):
                assert_equal(output_host[i], expected_buf[i])

    print("Butterfly pair swap test: passed")


def test_butterfly_parallel_max() raises:
    with DeviceContext() as ctx:
        var input_buf = ctx.enqueue_create_buffer[dtype](SIZE)
        input_buf.enqueue_fill(0)
        var output_buf = ctx.enqueue_create_buffer[dtype](SIZE)
        output_buf.enqueue_fill(0)

        with input_buf.map_to_host() as input_host:
            for i in range(SIZE):
                input_host[i] = Scalar[dtype](i * 2)
            # Make sure we have a clear maximum
            input_host[SIZE - 1] = 1000.0

        var input_tensor = TileTensor[mut=False, dtype, LayoutType](
            input_buf, layout
        )
        var output_tensor = TileTensor(output_buf, layout)

        comptime kernel = butterfly_parallel_max[SIZE]
        ctx.enqueue_function[kernel](
            output_tensor,
            input_tensor,
            grid_dim=BLOCKS_PER_GRID,
            block_dim=THREADS_PER_BLOCK,
        )

        ctx.synchronize()

        var expected_buf = ctx.enqueue_create_host_buffer[dtype](SIZE)
        expected_buf.enqueue_fill(1000.0)

        # All threads should have the maximum value (1000.0)
        with output_buf.map_to_host() as output_host:
            print("output:", output_host)
            print("expected:", expected_buf)

            for i in range(SIZE):
                assert_almost_equal(output_host[i], 1000.0, rtol=1e-5)

    print("Butterfly parallel max test: passed")


def test_butterfly_conditional_max() raises:
    with DeviceContext() as ctx:
        var input_buf = ctx.enqueue_create_buffer[dtype](SIZE_2)
        input_buf.enqueue_fill(0)
        var output_buf = ctx.enqueue_create_buffer[dtype](SIZE_2)
        output_buf.enqueue_fill(0)

        with input_buf.map_to_host() as input_host:
            for i in range(SIZE_2):
                if i < 9:
                    var values = [3, 1, 7, 2, 9, 4, 8, 5, 6]
                    input_host[i] = Scalar[dtype](values[i])
                else:
                    input_host[i] = Scalar[dtype](i % 10)

        var input_tensor = TileTensor[mut=False, dtype, Layout2Type](
            input_buf, layout_2
        )
        var output_tensor = TileTensor(output_buf, layout_2)

        comptime kernel = butterfly_conditional_max[SIZE_2]
        ctx.enqueue_function[kernel](
            output_tensor,
            input_tensor,
            grid_dim=BLOCKS_PER_GRID_2,
            block_dim=THREADS_PER_BLOCK_2,
        )

        ctx.synchronize()

        var expected_buf = ctx.enqueue_create_host_buffer[dtype](SIZE_2)
        expected_buf.enqueue_fill(0)

        # Expected: even lanes get max, odd lanes get min
        var max_val: Float32
        var min_val: Float32
        with input_buf.map_to_host() as input_host:
            max_val = input_host[0]
            min_val = input_host[0]
            for i in range(1, SIZE_2):
                if input_host[i] > max_val:
                    max_val = input_host[i]
                if input_host[i] < min_val:
                    min_val = input_host[i]

            for i in range(SIZE_2):
                if i % 2 == 0:
                    expected_buf[i] = max_val
                else:
                    expected_buf[i] = min_val

        with output_buf.map_to_host() as output_host:
            print("output:", output_host)
            print("expected:", expected_buf)

            for i in range(SIZE_2):
                if i % 2 == 0:
                    assert_almost_equal(output_host[i], max_val, rtol=1e-5)
                else:
                    assert_almost_equal(output_host[i], min_val, rtol=1e-5)

    print("Butterfly conditional max test: passed")


def test_warp_inclusive_prefix_sum() raises:
    with DeviceContext() as ctx:
        var input_buf = ctx.enqueue_create_buffer[dtype](SIZE)
        input_buf.enqueue_fill(0)
        var output_buf = ctx.enqueue_create_buffer[dtype](SIZE)
        output_buf.enqueue_fill(0)

        with input_buf.map_to_host() as input_host:
            for i in range(SIZE):
                input_host[i] = Scalar[dtype](i + 1)

        var input_tensor = TileTensor[mut=False, dtype, LayoutType](
            input_buf, layout
        )
        var output_tensor = TileTensor(output_buf, layout)

        comptime kernel = warp_inclusive_prefix_sum[SIZE]
        ctx.enqueue_function[kernel](
            output_tensor,
            input_tensor,
            grid_dim=BLOCKS_PER_GRID,
            block_dim=THREADS_PER_BLOCK,
        )

        var expected_buf = ctx.enqueue_create_host_buffer[dtype](SIZE)
        expected_buf.enqueue_fill(0)

        ctx.synchronize()

        # Create expected inclusive prefix sum: [1, 3, 6, 10, 15, 21, 28, 36, ...]
        with input_buf.map_to_host() as input_host:
            expected_buf[0] = input_host[0]
            for i in range(1, SIZE):
                expected_buf[i] = expected_buf[i - 1] + input_host[i]

        with output_buf.map_to_host() as output_host:
            print("output:", output_host)
            print("expected:", expected_buf)
            for i in range(SIZE):
                assert_almost_equal(output_host[i], expected_buf[i], rtol=1e-5)

    print("Warp inclusive prefix sum test: passed")


def test_warp_partition() raises:
    with DeviceContext() as ctx:
        var input_buf = ctx.enqueue_create_buffer[dtype](SIZE)
        input_buf.enqueue_fill(0)
        var output_buf = ctx.enqueue_create_buffer[dtype](SIZE)
        output_buf.enqueue_fill(0)

        # Create test data: mix of values above and below pivot
        var pivot_value = Scalar[dtype](5.0)
        with input_buf.map_to_host() as input_host:
            # Create: [3, 7, 1, 8, 2, 9, 4, 6, ...]
            var test_values = [
                3,
                7,
                1,
                8,
                2,
                9,
                4,
                6,
                0,
                10,
                3,
                11,
                1,
                12,
                4,
                13,
            ]
            for i in range(SIZE):
                input_host[i] = Scalar[dtype](test_values[i % len(test_values)])

        var input_tensor = TileTensor[mut=False, dtype, LayoutType](
            input_buf, layout
        )
        var output_tensor = TileTensor(output_buf, layout)

        comptime kernel = warp_partition[SIZE]
        ctx.enqueue_function[kernel](
            output_tensor,
            input_tensor,
            pivot_value,
            grid_dim=BLOCKS_PER_GRID,
            block_dim=THREADS_PER_BLOCK,
        )

        var expected_buf = ctx.enqueue_create_host_buffer[dtype](SIZE)
        expected_buf.enqueue_fill(0)

        ctx.synchronize()

        # Create expected results: elements < 5 on left, >= 5 on right
        with input_buf.map_to_host() as input_host:
            var left_values = List[Float32]()
            var right_values = List[Float32]()

            for i in range(SIZE):
                if input_host[i] < pivot_value:
                    left_values.append(input_host[i])
                else:
                    right_values.append(input_host[i])

            # Fill expected buffer
            for i in range(len(left_values)):
                expected_buf[i] = left_values[i]
            for i in range(len(right_values)):
                expected_buf[len(left_values) + i] = right_values[i]

        with output_buf.map_to_host() as output_host:
            print("output:", output_host)
            print("expected:", expected_buf)
            print("pivot:", pivot_value)

            # Verify partitioning property (left < pivot, right >= pivot)
            # Find partition boundary
            var partition_point = 0
            for i in range(SIZE):
                if output_host[i] >= pivot_value:
                    partition_point = i
                    break

            # Check left partition
            for i in range(partition_point):
                if output_host[i] >= pivot_value:
                    print("ERROR: Left partition contains value >= pivot")

            # Check right partition
            for i in range(partition_point, SIZE):
                if output_host[i] < pivot_value:
                    print("ERROR: Right partition contains value < pivot")

    print("Warp partition test: passed")


def main() raises:
    print("WARP_SIZE: ", WARP_SIZE)
    if len(argv()) != 2:
        print(
            "Usage: p24.mojo"
            " [--pair-swap|--parallel-max|--conditional-max|--prefix-sum|--partition]"
        )
        return

    var test_type = argv()[1]
    if test_type == "--pair-swap":
        print("SIZE: ", SIZE)
        test_butterfly_pair_swap()
        print("Puzzle 26 complete ✅")
    elif test_type == "--parallel-max":
        print("SIZE: ", SIZE)
        test_butterfly_parallel_max()
        print("Puzzle 26 complete ✅")
    elif test_type == "--conditional-max":
        print("SIZE: ", SIZE_2)
        test_butterfly_conditional_max()
        print("Puzzle 26 complete ✅")
    elif test_type == "--prefix-sum":
        print("SIZE: ", SIZE)
        test_warp_inclusive_prefix_sum()
        print("Puzzle 26 complete ✅")
    elif test_type == "--partition":
        print("SIZE: ", SIZE)
        test_warp_partition()
        print("Puzzle 26 complete ✅")
    else:
        print(
            "Usage: p24.mojo"
            " [--pair-swap|--parallel-max|--conditional-max|--prefix-sum|--partition]"
        )
